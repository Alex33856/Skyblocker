import net.fabricmc.loom.task.ValidateAccessWidenerTask

plugins {
	// Building
	id 'net.fabricmc.fabric-loom-remap'
	id "de.hysky.skyblocker.annotation-processor"
	id "checkstyle"
	// Publishing
	id 'maven-publish'
	id "me.modmuss50.mod-publish-plugin" version "${mod_publish_plugin_version}"
}

version = "${project.mod_version}+${sc.current.version}"
group = project.maven_group

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.
	flatDir {
		dirs 'libs'
	}

	maven {
		name = 'Maven Central'
		url = 'https://repo.maven.apache.org/maven2'
	}

	// For ModMenu and EMI
	exclusiveContent {
		forRepository {
			maven { url = "https://maven.terraformersmc.com/releases" }
		}

		filter {
			includeGroup "com.terraformersmc"
			includeGroup "dev.emi"
		}
	}

	// For REI
	exclusiveContent {
		forRepository {
			maven { url = "https://maven.shedaniel.me/" }
		}

		filter {
			includeGroup "me.shedaniel"
			includeGroup "me.shedaniel.cloth"
			includeGroup "dev.architectury"
		}
	}

	// For JEI
	exclusiveContent {
		forRepository {
			maven {
				name = "Modrinth"
				url = "https://api.modrinth.com/maven"
			}
		}

		filter {
			includeGroup "maven.modrinth"
		}
	}

	// YACL
	maven {
		url = "https://maven.isxander.dev/releases"

		content {
			includeGroup "dev.isxander"
			includeGroup "org.quiltmc.parsers"
		}
	}

	// For Minecraft snapshots
	maven {
		url = "https://maven.isxander.dev/snapshots"

		content {
			includeGroup "dev.isxander"
		}
	}

	// For Discord RPC
	exclusiveContent {
		forRepository {
			maven {
				name = "meteor-maven"
				url = "https://maven.meteordev.org/releases"
			}
		}

		filter {
			includeGroup "meteordevelopment"
		}
	}

	// For NEU repo parser
	exclusiveContent {
		forRepository {
			maven { url = "https://repo.nea.moe/releases" }
		}

		filter {
			includeGroup "moe.nea"
		}
	}

	maven {
		url = "https://maven.azureaaron.net/releases"

		content {
			includeGroup "net.azureaaron"
		}
	}

	maven {
		url = "https://maven.azureaaron.net/snapshots"

		content {
			includeGroup "net.azureaaron"
		}
	}

	maven {
		url = "https://maven.notenoughupdates.org/releases"

		content {
			includeGroup "org.notenoughupdates.moulconfig"
		}
	}

	//mavenLocal()
}

dependencies {
	testImplementation "net.fabricmc:fabric-loader-junit:${project.loader_version}"
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${sc.current.version}"

	if (stonecutter.current.version == "1.21.10") {
		mappings loom.layered {
			officialMojangMappings()
			mappings file("skyblocker-override.tiny")
		}
	} else {
		mappings loom.officialMojangMappings()
	}

	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Loom does not always add this
	implementation "ca.weblite:java-objc-bridge:1.1"

	// Fabric API
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"

	// Dandelion (also shades in MoulConfig)
	include modImplementation("net.azureaaron:dandelion:${project.dandelion_version}")

	// YACL
	include modImplementation("dev.isxander:yet-another-config-lib:${project.yacl_version}-fabric")

	// HM API (Hypixel Mod API Library)
	include modImplementation("net.azureaaron:hm-api:${project.hm_api_version}")

	// Mod Menu
	modImplementation "com.terraformersmc:modmenu:${project.mod_menu_version}"

	// REI
	modCompileOnly("me.shedaniel:RoughlyEnoughItems-api-fabric:${project.rei_version}") {
		exclude group: "net.fabricmc.fabric-api" //Stop this from importing an old version of FAPI that crashes everything
	}
	//modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}"

	// EMI
	modCompileOnly "dev.emi:emi-fabric:${project.emi_version}:api"
	//modLocalRuntime "dev.emi:emi-fabric:${project.emi_version}"

	// JEI (Using modrinth repo since official release is in mojmap and doesn't work)
	modCompileOnly "maven.modrinth:jei:${project.jei_version}-fabric"
	//modRuntimeOnly "maven.modrinth:jei:${project.jei_version}-fabric"

	compileOnly "com.demonwav.mcdev:annotations:${project.mcdev_annotations_version}"

	include modImplementation("meteordevelopment:discord-ipc:1.1")

	// NEU RepoParser
	include implementation("moe.nea:neurepoparser:${project.repoparser_version}")

	// Networth Calculator (https://github.com/AzureAaron/networth-calculator)
	include implementation("net.azureaaron:networth-calculator:${project.networth_calculator_version}")

	// Legacy Item DFU
	include implementation("net.azureaaron:legacy-item-dfu:${project.legacy_item_dfu_version}")

	// JGit used pull data from the NEU item repo
	include implementation("org.eclipse.jgit:org.eclipse.jgit:${project.jgit_version}")

	// Apache Commons Math
	include implementation("org.apache.commons:commons-math3:${project.commons_math_version}")

	// Apache Commons Text
	include implementation("org.apache.commons:commons-text:${project.commons_text_version}")

	if (stonecutter.current.version == '1.21.10') {
		// JSpecify Annotations
		include implementation("org.jspecify:jspecify:1.0.0")

		// FastUtil
		include implementation("it.unimi.dsi:fastutil:8.5.18")
	}

	// Fabric Language Kotlin for using MoulConfig - we are still a Java-exclusive mod
	modImplementation("net.fabricmc:fabric-language-kotlin:${project.flk_version}")
}

loom {
	accessWidenerPath = sc.process(rootProject.file("src/main/resources/skyblocker.classtweaker"), "build/skyblocker.accesswidener")

	mixin {
		useLegacyMixinAp = false
	}

	decompilers {
		vineflower {
			options.put(
					// Makes lambda names easy to find for Mixins
					"mark-corresponding-synthetics", "1"
					)
		}
	}

	runConfigs.all {
		ideConfigGenerated(true)
		runDir = "../../run" // Shares the run directory between versions
	}
}

fabricApi {
	configureTests {
		createSourceSet = true
		modId = "skyblocker-test"
		enableGameTests = false
		eula = true
	}
}

base {
	archivesName = project.archives_base_name
}

tasks.withType(ProcessResources).configureEach {
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version,
		"fabric_api_version": project.property("fabric_api_version"),
		"dandelion_version": project.property("dandelion_version"),
		"yacl_version": project.property("yacl_version"),
		"minecraft_version": sc.current.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
	if (stonecutter.current.version == "1.21.10") {
		it.options.compilerArgs << "-Xmaxerrs" << "2000"
	}
}

java {
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	from("LICENSE") {
		rename { "${it}_${base.archivesName.get()}" }
	}
}

test {
	useJUnitPlatform()

	systemProperty("IS_TEST_ENV", "true")
}

checkstyle {
	toolVersion = "${checkstyle_version}"
	configFile = rootProject.file("checkstyle.xml")
	configProperties = [
		"suppressionsFile": rootProject.file("checkstyle-suppressions.xml"),
		"cacheFile": layout.buildDirectory.file("checkstyle/checkstyle-cache.txt").get().asFile
	]
}

def truncateChangelog = { String text, int limit ->
	if (!text) return ''
	def lines = text.readLines()
	def sb = new StringBuilder()
	for (line in lines) {
		def toAdd = (sb.length() == 0 ? "" : "\n") + line
		if (sb.length() + toAdd.length() > limit) {
			sb.append("\nand more!")
			break
		}
		sb.append(toAdd)
	}
	return sb.toString()
}

publishMods {
	file = remapJar.archiveFile
	changelog = System.getenv('CHANGELOG_HIGHLIGHT')
	version = "v${project.version}"
	displayName = "Skyblocker ${mod_version} for ${sc.current.version}"
	modLoaders.add("fabric")
	type = project.version.contains("alpha") ? ALPHA : project.version.contains("beta") ? BETA : STABLE

	github {
		accessToken = System.getenv("GITHUB_TOKEN")
		repository = "SkyblockerMod/Skyblocker"
		commitish = System.getenv("REF_NAME")
		tagName = publishMods.version
		changelog = System.getenv("CHANGELOG")
	}

	modrinth {
		accessToken = System.getenv("MODRINTH_TOKEN")
		projectId = modrinth_id
		minecraftVersions.add(sc.current.version)
		announcementTitle = "<:modrinth:1237114573354438696> Download from Modrinth"
		requires("fabric-api", "fabric-language-kotlin")
		optional("modmenu", "rei", "emi", "jei")
		embeds("yacl")
	}

	curseforge {
		accessToken = System.getenv("CURSEFORGE_TOKEN")
		projectId = curseforge_id
		minecraftVersions.add(sc.current.version)
		announcementTitle = "<:curseforge:900697838453936149> Download from CurseForge"
		projectSlug = "skyblocker"
		requires("fabric-api", "fabric-language-kotlin")
		optional("roughly-enough-items", "emi", "jei")
		embeds("yacl")
	}

	discord {
		webhookUrl = System.getenv("DISCORD_WEBHOOK")
		username = "Changelog"
		content = changelog.map {
			def header = "<@&1134565945482948638>\n## Skyblocker v${mod_version} for ${sc.current.version}\n"
			def truncatedChangelog = truncateChangelog(it, 1850)
			header + truncatedChangelog
		}
		setPlatforms(publishMods.platforms.curseforge, publishMods.platforms.modrinth)
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
