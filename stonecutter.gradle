import com.diffplug.spotless.LineEnding

plugins {
	id("dev.kikugie.stonecutter")
	id("net.fabricmc.fabric-loom-remap") version "1.14-SNAPSHOT" apply false
	id "com.diffplug.spotless" version "${spotless_version}"
}

repositories {
	mavenCentral()
}

stonecutter.active "1.21.11"

// Remove when natively supported by Stonecutter
stonecutter.handlers {
	inherit("accesswidener", "classtweaker")
}

stonecutter.parameters {
	replacements {
		string(current.parsed.matches(">1.21.11")) {
			// Fabric API Renames
			replace("net.fabricmc.fabric.api.client.command.v2.ClientCommandManager", "net.fabricmc.fabric.api.client.command.v2.ClientCommands")
			replace("ClientCommandManager", "ClientCommands")
			replace("getSource().getWorld()", "getSource().getLevel()")
			replace("source.getWorld()", "source.getLevel()")

			replace("net.fabricmc.fabric.api.client.keybinding.v1.KeyBindingHelper", "net.fabricmc.fabric.api.client.keymapping.v1.KeyMappingHelper")
			replace("KeyBindingHelper", "KeyMappingHelper")
			replace("registerKeyBinding", "registerKeyMapping")

			replace("net.fabricmc.fabric.api.renderer.v1.render.RenderLayerHelper", "net.fabricmc.fabric.api.renderer.v1.render.ChunkSectionLayerHelper")
			replace("RenderLayerHelper", "ChunkSectionLayerHelper")

			replace("net.fabricmc.fabric.api.client.rendering.v1.world.WorldRenderEvents", "net.fabricmc.fabric.api.client.rendering.v1.level.LevelRenderEvents")
			replace("WorldRenderEvents.BEFORE_ENTITIES", "LevelRenderEvents.COLLECT_SUBMITS")
			replace("WorldRenderEvents", "LevelRenderEvents")
			replace("net.fabricmc.fabric.api.client.rendering.v1.world.WorldTerrainRenderContext", "net.fabricmc.fabric.api.client.rendering.v1.level.LevelTerrainRenderContext")
			replace("WorldTerrainRenderContext", "LevelTerrainRenderContext")
			replace("net.fabricmc.fabric.api.client.rendering.v1.world.WorldExtractionContext", "net.fabricmc.fabric.api.client.rendering.v1.level.LevelExtractionContext")
			replace("WorldExtractionContext", "LevelExtractionContext")
			replace("net.fabricmc.fabric.api.client.rendering.v1.world.WorldRenderContext", "net.fabricmc.fabric.api.client.rendering.v1.level.LevelRenderContext")
			replace("WorldRenderContext", "LevelRenderContext")
			replace("context.worldState()", "context.levelState()")
			replace("context.commandQueue()", "context.submitNodeCollector()")

			replace("net.fabricmc.fabric.api.client.rendering.v1.SpecialGuiElementRegistry", "net.fabricmc.fabric.api.client.rendering.v1.PictureInPictureRendererRegistry")
			replace("SpecialGuiElementRegistry", "PictureInPictureRendererRegistry")
			replace("context.vertexConsumers()", "context.bufferSource()")

			replace("VanillaHudElements.STATUS_EFFECTS", "VanillaHudElements.MOB_EFFECTS")
			replace("ClientTickEvents.END_WORLD_TICK", "ClientTickEvents.END_LEVEL_TICK")
			replace("Screens.getButtons", "Screens.getWidgets")
			replace("registerReloader", "registerReloadListener")

			// Minecraft Renames
			replace("net.minecraft.world.inventory.ClickType", "net.minecraft.world.inventory.ContainerInput")
			replace("ClickType", "ContainerInput")
			replace("handleInventoryMouseClick", "handleContainerInput")
			replace("net.minecraft.client.renderer.LightTexture", "net.minecraft.util.LightCoordsUtil")
			replace("LightTexture", "LightCoordsUtil")
			replace("scrollbarVisible()", "scrollable()")
			replace("getDayTime()", "getOverworldClockTime()") // TODO (26.1): test this
			replace("ItemTags.DYEABLE", "ItemTags.CAULDRON_CAN_REMOVE_DYE")
			replace("getChat().addMessage(", "getChat().addClientSystemMessage(")
		}

		// regex replacements are evil
		regex(current.parsed.matches(">1.21.11")) {
			replace("displayClientMessage\\(([a-zA-Z0-9\\t\\n.()_\\->\"' ,+!\\[\\]%ยง?\\/:]+),(?: |\\n\\t+)?false(?:\\n\\t+)?\\)",
					"sendSystemMessage(\$1)",
					"sendSystemMessage\\(([a-zA-Z0-9\\t\\n.()_\\->\"' ,+!\\[\\]%ยง?\\/:]+)(?: |\\n\\t+)?\\)",
					"displayClientMessage(\$1, false)"
			)
			replace("displayClientMessage\\(([a-zA-Z0-9\\t\\n.()_\\->\"' ,+!\\[\\]%ยง?\\/:]+),(?: |\\n\\t+)?true(?:\\n\\t+)?\\)",
					"sendOverlayMessage(\$1)",
					"sendOverlayMessage\\(([a-zA-Z0-9\\t\\n.()_\\->\"' ,+!\\[\\]%ยง?\\/:]+)(?: |\\n\\t+)?\\)",
					"displayClientMessage(\$1, true)"
			)
		}

		string(current.parsed.matches("<1.21.11")) {
			replace("net.azureaaron.dandelion.", "net.azureaaron.dandelion_bp.")
		}
	}
}

spotless {
	java {
		target("src/**/*.java", "versions/*/src/**/*.java")
		removeUnusedImports()
		leadingSpacesToTabs()
		trimTrailingWhitespace()
		endWithNewline()
		lineEndings = LineEnding.UNIX
	}

	json {
		target 'src/**/lang/en_us.json'
		targetExclude 'src/**/generated/**'

		gson().sortByKeys().indentWithSpaces(2)
		// Note: a stem is a part of a key between dots, e.g. "skyblocker.config.chat" has three stems: "skyblocker", "config", and "chat".
		replaceRegex  \
				 "Separate different first stems",
				// Explanation of the regex:
				// `^(  "`: matches the start of a line, begins capture group 1, and matches two spaces and a double quote
				// `([^."]++)`: captures the first stem (everything before the first dot) in group 2
				// `(?:\.[^"]++)?`: optionally matches a second stem (everything after the first stem before the next double quote)
				// `": "`: matches the end of the key and the start of the value
				// `(?:[^\\"]++|\\.)*+`: matches the value, which can be anything except a slash or double quote, special processing here for escaped characters
				// `",)$`: matches the end of the line, capturing the whole line in group 1
				// `\n`: matches a newline character
				// `^(?=  "(?!\2[."]))`: positive lookahead to ensure the next line starts with two spaces and a key that does not start with the same first stem (group 2) followed by a dot or a double quote
				// The last `[."]` is to ensure that the `\2` in the negative lookahead is being matched against an entire stem, not just the starting part of a stem.
				/^(  "([^."]++)(?:\.[^"]++)?": "(?:[^\\"]++|\\.)*+",)$\n^(?=  "(?!\2[."]))/,
				'$1\n\n'
		replaceRegex  \
				 "Separate different second stems if the first stem is skyblocker",
				// The main difference is that the second and remaining stems are optional, allowing matching lines with only one stem (in this case "skyblocker"), and separating it from other keys that start with "skyblocker.[whatever]".
				// This treats keys with only one stem as if they have an empty second stem, which is considered different from any other second stem, therefore inserting a newline after them.
				/^(  "skyblocker(?:\.([^."]++)(?:\.[^"]++)?)?": "(?:[^\\"]++|\\.)*+",)$\n^(?=  "skyblocker\.(?!\2[."]))/,
				'$1\n\n'
		replaceRegex  \
				 "Separate different thrid stems if the first two stems are skyblocker.config",
				// The third and remaining stems are optional, allowing matching lines with only two stems (in this case "skyblocker.config"), and separating it from other keys that start with "skyblocker.config.[whatever]".
				/^(  "skyblocker\.config(?:\.([^."]++)(?:\.[^"]++)?)?": "(?:[^\\"]++|\\.)*+",)$\n^(?=  "skyblocker\.config\.(?!\2[."]))/,
				'$1\n\n'
		final List<String> excludeCategories = ["debug", "eventNotifications", "quickNav", "shortcutToKeybindsSettings", "title"]
		final String excludeCategoriesLookahead = /(?!(?:${excludeCategories.join('|')})(?=[."]))/
		replaceRegex  \
				 "Separate different fouth stems if the first three stems are skyblocker.config.[configCategory]",
				// A negative lookahead is used before the third stem to exclude the above list of third stems.
				// The fourth and remaining stems are optional, allowing matching lines with only three stems (such as "skyblocker.config.[configCategory]"), and separating them from other keys that start with "skyblocker.config.[configCategory].[whatever]".
				// This treats keys with only three stems as if they have an empty fourth stem, which is considered different from any other fourth stem, therefore inserting a newline after them.
				// Specifically, the `(?!\3[."])` part ensures that the next line does not start with the same fourth stem (group 3) followed by a dot or a double quote.
				// If the previous line does not have a fourth stem, group 3 will be empty, and `(?!\3[."])` will attempt to match a dot or a double quote, effectively trying to apply a negative look ahead against an empty fourth stem, which will always succeed against "skyblocker.config.[configCategory].[whatever]".
				/^(  "skyblocker\.config\.${excludeCategoriesLookahead}([^."]++)(?:\.([^."]++)(?:\.[^"]++)?)?": "(?:[^\\"]++|\\.)*+",)$\n^(?=  "skyblocker\.config\.\2\.(?!\3[."]))/,
				'$1\n\n'
	}

	groovyGradle {
		target 'src/**/*.gradle', '*.gradle', 'gradle/*.gradle'
		greclipse().configProperties  \
				 'groovy.formatter.multiline.indentation=2', // Default
				'groovy.formatter.line.maxlength=999',
				'groovy.formatter.longListLength=999',
				'groovy.formatter.remove.unnecessary.semicolons=true'
	}
}
